archivesBaseName = "polymer-blocks"
version = rootProject.mod_version + "+" + rootProject.minecraft_version


sourceSets {
    testmod {
        runtimeClasspath += main.runtimeClasspath
        compileClasspath += main.compileClasspath
    }
}

loom {
    runs {
        testmodClient {
            client()
            ideConfigGenerated project.rootProject == project
            name = "(Blocks) Test Mod Client"
            source sourceSets.testmod
        }
        testmodServer {
            server()
            ideConfigGenerated project.rootProject == project
            name = "(Blocks) Test Mod Server"
            source sourceSets.testmod
        }
    }
}

dependencies {
    modRuntimeOnly "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"
    modCompileOnly "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

    implementation(project(path: ':polymer-core', configuration: 'namedElements'))
    implementation(project(path: ':polymer-common', configuration: 'namedElements'))
    implementation(project(path: ':polymer-resource-pack', configuration: 'namedElements'))
    compileOnly project(path: ":polymer-resource-pack-extras", configuration: "namedElements")

    modCompileOnly "me.lucko:fabric-permissions-api:0.1-SNAPSHOT"

    //modLocalRuntime "fr.catcore:server-translations-api:1.4.9+1.18.2-rc1"
    //modCompileOnly "fr.catcore:server-translations-api:1.4.9+1.18.2-rc1"

    //modCompileOnly 'nl.theepicblock:PolyMc:PolyMc:5.1.0+1.19'
    modCompileOnly ('com.github.TheEpicBlock:PolyMc:5.4.0') {
        exclude(group: "org.quiltmc", module: "quilt-loader")
    }
    testmodImplementation sourceSets.main.output
    //testmodImplementation project(":polymer-core").sourceSets.testmod.output
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}